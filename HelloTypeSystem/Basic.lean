import ¬´HelloTypeSystem¬ª.Util

namespace HelloTypeSystem

/-!
# Ë´∏ÂÆöÁæ©

## „Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞PNat
-/

/--
„Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞
$$\begin{align*}
\Set{PNat} \ni \MV{n} ::={}& \TT{Z} \mid \TT{S}\MV{n} \\\\
\end{align*}$$
-/
inductive PNat
  | Z
  | S (n : PNat)

instance : OfNat PNat 0 where
  ofNat := PNat.Z

instance (n : Nat) [OfNat PNat n] : OfNat PNat (Nat.succ n) where
  ofNat := PNat.S (OfNat.ofNat n)

def PNat.toNat : PNat ‚Üí Nat
  | .Z   => Nat.zero
  | .S n => Nat.succ n.toNat

instance : Coe PNat Nat where
  coe n := n.toNat

instance : ToString PNat where
  toString n := s!"{n.toNat}"

namespace PNat

/--
`plus`„ÅØ„Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞‰∏ä„ÅÆÂä†ÁÆóÈñ¢Êï∞„Åß„ÅÇ„Çã„ÄÇ
-/
def plus : PNat ‚Üí PNat ‚Üí PNat
  | .Z,    n‚ÇÇ => n‚ÇÇ
  | .S n‚ÇÅ, n‚ÇÇ => .S <| plus n‚ÇÅ n‚ÇÇ

/--
Âä†ÁÆóÈñ¢Êï∞`plus`„ÅÆÂ∑¶ÂÖ®ÂüüÊÄß
-/
theorem plus_left_total : ‚àÄ {n‚ÇÅ n‚ÇÇ : PNat}, ‚àÉ n‚ÇÉ : PNat, plus n‚ÇÅ n‚ÇÇ = n‚ÇÉ
  | .Z,    n‚ÇÇ => ‚ü®n‚ÇÇ, rfl‚ü©
  | .S n‚ÇÅ, n‚ÇÇ => ‚ü®.S <| plus n‚ÇÅ n‚ÇÇ, rfl‚ü©

/--
Âä†ÁÆóÈñ¢Êï∞`plus`„ÅÆ‰∏ÄÊÑèÊÄß
-/
theorem plus_uniq : plus n‚ÇÅ n‚ÇÇ = n‚ÇÉ ‚Üí plus n‚ÇÅ n‚ÇÇ = n‚ÇÉ' ‚Üí n‚ÇÉ = n‚ÇÉ'
  | d, d' => Eq.trans d.symm d'

end PNat

/-!
## ÁÆóË°ìÂºèExpr
-/

/--
ÁÆóË°ìÂºè
$$\begin{align*}
\Set{Expr} \ni \MV{e} ::={}& \MV{n} \mid \TT{$\MV{e}$ + $\MV{e}$} \mid \TT{$\MV{e}$ * $\MV{e}$} \\\\
\end{align*}$$
-/
inductive Expr where
  | Nat (n : PNat)
  | Add (e‚ÇÅ e‚ÇÇ : Expr)
  | Mul (e‚ÇÅ e‚ÇÇ : Expr)

instance : Coe PNat Expr where
  coe := .Nat
instance : Add Expr where
  add := .Add
instance : Mul Expr where
  mul := .Mul

instance [OfNat PNat n] : OfNat Expr n where
  ofNat := Expr.Nat (OfNat.ofNat n)

namespace Expr

/--
`size`„ÅØÁÆóË°ìÂºè„ÅÆÂ§ß„Åç„Åï„Çí‰∏é„Åà„Çã„ÄÇ$\newcommand\Size{\mathord{\mathit{size}}}$
-/
def size : Expr ‚Üí _root_.Nat
  | .Nat .Z     => 1
  | .Nat (.S n) => size n + 1
  | .Add e‚ÇÅ e‚ÇÇ  => e‚ÇÅ.size + e‚ÇÇ.size + 1
  | .Mul e‚ÇÅ e‚ÇÇ  => e‚ÇÅ.size + e‚ÇÇ.size + 1

/--
`size`„ÅÆÂ∑¶ÂÖ®ÂüüÊÄß
-/
theorem size_left_total : ‚àÄ {e : Expr}, ‚àÉ n, e.size = n
  | .Nat .Z     => ‚ü®1, rfl‚ü©
  | .Nat (.S n) => ‚ü®size n + 1, by simp [size]‚ü©
  | .Add e‚ÇÅ e‚ÇÇ  => ‚ü®e‚ÇÅ.size + e‚ÇÇ.size + 1, by simp [size]‚ü©
  | .Mul e‚ÇÅ e‚ÇÇ  => ‚ü®e‚ÇÅ.size + e‚ÇÇ.size + 1, by simp [size]‚ü©

/--
`size`„ÅÆ‰∏ÄÊÑèÊÄß
-/
theorem size_uniq {e : Expr} : e.size = n ‚Üí e.size = n' ‚Üí n = n'
  | h, h' => Eq.trans h.symm h'

/--
`height`„ÅØÁÆóË°ìÂºè„ÅÆÈ´ò„Åï„Çí‰∏é„Åà„Çã„ÄÇ$\newcommand\Height{\mathord{\mathit{height}}}$
-/
def height : Expr ‚Üí _root_.Nat
  | .Nat .Z     => 1
  | .Nat (.S n) => height n + 1
  | .Add e‚ÇÅ e‚ÇÇ  => max e‚ÇÅ.height e‚ÇÇ.height + 1
  | .Mul e‚ÇÅ e‚ÇÇ  => max e‚ÇÅ.height e‚ÇÇ.height + 1

/--
`height`„ÅÆÂ∑¶ÂÖ®ÂüüÊÄß
-/
theorem height_left_total : ‚àÄ {e : Expr}, ‚àÉ n, e.height = n
  | .Nat .Z     => ‚ü®1, rfl‚ü©
  | .Nat (.S n) => ‚ü®height n + 1, by simp [height]‚ü©
  | .Add e‚ÇÅ e‚ÇÇ  => ‚ü®max e‚ÇÅ.height e‚ÇÇ.height + 1, by simp [height]‚ü©
  | .Mul e‚ÇÅ e‚ÇÇ  => ‚ü®max e‚ÇÅ.height e‚ÇÇ.height + 1, by simp [height]‚ü©

/--
`height`„ÅÆ‰∏ÄÊÑèÊÄß
-/
theorem height_uniq {e : Expr} : e.height = n ‚Üí e.height = n' ‚Üí n = n'
  | h, h' => Eq.trans h.symm h'

/--
$$\forall\MV{e}\in\Set{Expr}. \Size(\MV{e}) \le 2^{\Height(\MV{e})} - 1.$$
-/
theorem size_le_prev_pow_2_height : (e : Expr) ‚Üí e.size ‚â§ 2^e.height - 1
  | .Nat .Z =>
      calc
        _ ‚â§ 1     := Nat.le_refl 1
        _ = 2 - 1 := Nat.sub_one 2
  | .Nat (.S n) =>
      calc
        _ = size n + 1           := by simp [size]
        _ ‚â§ 2 ^ height n - 1 + 1 := Nat.succ_le_succ (size_le_prev_pow_2_height n)
        _ = 2 ^ height n + 1 - 1 := Nat.sub_add_comm Nat.one_le_two_pow |> .symm
        _ ‚â§ 2 ^ height n.S   - 1
          := (Nat.sub_le_sub_right ¬∑ 1) <|
            calc  2 ^ height n + 1
              _ ‚â§ 2 ^ height n + 2 ^ height n := Nat.add_le_add_left Nat.one_le_two_pow _
              _ = 2 * 2 ^ height n            := Nat.add_same
              _ = 2 ^ (height n + 1)          := Nat.pow_succ' |> .symm
              _ = 2 ^ height n.S              := by simp [height]
  | .Add e‚ÇÅ e‚ÇÇ =>
      calc
        _ = e‚ÇÅ.size + e‚ÇÇ.size + 1 := by simp [size]
        _ ‚â§ 2 ^ (max e‚ÇÅ.height e‚ÇÇ.height + 1) - 2 + 1
          := (Nat.add_le_add_right ¬∑ 1) <|
              calc e‚ÇÅ.size + e‚ÇÇ.size
                _ ‚â§ 2 ^ max e‚ÇÅ.height e‚ÇÇ.height - 1 + e‚ÇÇ.size
                  := (Nat.add_le_add_right ¬∑ _) <|
                      calc e‚ÇÅ.size
                        _ ‚â§ 2 ^ e‚ÇÅ.height - 1 := size_le_prev_pow_2_height e‚ÇÅ
                        _ ‚â§ _
                          := (Nat.sub_le_sub_right ¬∑ 1) <|
                              Nat.pow_le_pow_of_le Nat.one_lt_two (Nat.le_max_left ..)
                _ ‚â§ 2 ^ max e‚ÇÅ.height e‚ÇÇ.height - 1 + (2 ^ max e‚ÇÅ.height e‚ÇÇ.height - 1)
                  := (Nat.add_le_add_left ¬∑ _) <|
                      calc e‚ÇÇ.size
                        _ ‚â§ 2 ^ e‚ÇÇ.height - 1 := size_le_prev_pow_2_height e‚ÇÇ
                        _ ‚â§ _
                          := (Nat.sub_le_sub_right ¬∑ 1) <|
                              Nat.pow_le_pow_of_le Nat.one_lt_two (Nat.le_max_right ..)
                _ = 2 * 2 ^ max e‚ÇÅ.height e‚ÇÇ.height - 2
                  := calc
                        _ = 2 ^ max e‚ÇÅ.height e‚ÇÇ.height + (2 ^ max e‚ÇÅ.height e‚ÇÇ.height - 1) - 1
                          := Nat.sub_add_comm (Nat.one_le_two_pow) |> .symm
                        _ = 2 ^ max e‚ÇÅ.height e‚ÇÇ.height + 2 ^ max e‚ÇÅ.height e‚ÇÇ.height - 2
                          := congrArg (¬∑ - 1) <| Nat.add_sub_assoc Nat.one_le_two_pow _ |> .symm
                        _ = _ := congrArg (¬∑ - 2) Nat.add_same
                _ = 2 ^ (max e‚ÇÅ.height e‚ÇÇ.height + 1) - 2
                  := congrArg (¬∑ - 2) Nat.pow_succ'.symm
        _ = 2 ^ (max e‚ÇÅ.height e‚ÇÇ.height + 1) - 1
          := calc
                _ = 2 ^ (max e‚ÇÅ.height e‚ÇÇ.height + 1) + 1 - 2
                  := Nat.sub_add_comm (
                      (Nat.pow_le_pow_iff_right Nat.one_lt_two).mpr <| Nat.succ_le_succ (Nat.zero_le _)
                    ) |> .symm
                _ = _
                  := Nat.add_sub_add_right _ 1 1
        _ = _ := by simp [height]
  | .Mul e‚ÇÅ e‚ÇÇ => sorry

end Expr

/-!
## Âà§Êñ≠ÔºàjudgementÔºâ
-/

/--
Âà§Êñ≠

„Åì„ÅÆÂûã„ÅÆÈ†Ö„ÅØÂΩ¢Âºè‰∏ä„ÅØÊ≠£„Åó„ÅÑÂà§Êñ≠„Åß„ÅÇ„Çã„Åå„ÄÅÂÜÖÂÆπÁöÑ„Å´„ÇÇÊ≠£„Åó„ÅÑ„Å®„ÅØÈôê„Çâ„Å™„ÅÑ„ÄÇ
-/
inductive Judgement where
  /--
  "$\TT{$\MV{n_1}$ plus $\MV{n_2}$ is $\MV{n_3}$}$"
  -/
  | Plus (n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat)
  /--
  "$\TT{$\MV{n_1}$ times $\MV{n_2}$ is $\MV{n_3}$}$"
  -/
  | Times (n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat)
  /--
  "$\TT{$\MV{n_1}$ is less than $\MV{n_2}$}$"
  -/
  | LT (n‚ÇÅ n‚ÇÇ : PNat)
  /--
  "$\MV{e} \Evals \MV{n}$" means that $\MV{e}$ evaluates to $\MV{n}$.
  -/
  | Eval (e : Expr) (n : PNat)
  /--
  "$\MV{e} \Reduces \MV{e'}$" means that $\MV{e}$ is reduced to $\MV{e'}$ at a time.
  -/
  | Reduce (e : Expr) (e' : Expr)
  /--
  "$\MV{e} \MReduces \MV{e'}$" means that $\MV{e}$ is reduced to $\MV{e'}$ at some time.
  -/
  | MReduce (e : Expr) (e' : Expr)
  /--
  "$\MV{e} \DReduces \MV{e'}$" means that $\MV{e}$ is reduced to $\MV{e'}$ deterministically.
  -/
  | DReduce (e : Expr) (e' : Expr)

notation:50 e:51 " ‚áì " n:51  => Judgement.Eval e n
notation:50 e:51 " ‚ü∂ " e':51 => Judgement.Reduce e e'
notation:50 e:51 " ‚ü∂* " e':51 => Judgement.MReduce e e'
notation:50 e:51 " ‚ü∂' " e':51 => Judgement.DReduce e e'

/--
‰∏é„Åà„Çâ„Çå„ÅüÂà§Êñ≠„ÅåÂ∞éÂá∫„Åß„Åç„Çã„Å®„ÅÑ„ÅÜÂëΩÈ°å
-/
inductive Derivable {Derivation : Judgement ‚Üí Type v} (ùí• : Judgement) : Prop where
  | intro (h : Derivation ùí•)

/--
Â∞éÂá∫„ÅÆÈ†Ö„ÅåÊßãÁØâ„Åß„Åç„Åü„Å®„Åç„ÅØÊòé„Çâ„Åã„Å´Â∞éÂá∫„Åß„Åç„Çã„ÅÆ„ÅßÂûãÂº∑Âà∂„Åô„Çã
-/
instance {ùí• : Judgement} {Derivation : Judgement ‚Üí Type u} : Coe (Derivation ùí•) (Derivable (Derivation := Derivation) ùí•) where
  coe := Derivable.intro

/-!
## Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†
-/

/-!
### „Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞„ÅÆÂä†ÁÆó„Éª‰πóÁÆóÔºöNat
-/
namespace Nat
/--
Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†Nat„ÅÆÊé®Ë´ñË¶èÂâá„Å´„Çà„ÇãÂ∞éÂá∫
-/
inductive Derivation : Judgement ‚Üí Type where
  /--
  ‰ªªÊÑè„ÅÆ„Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞$\MV{n}$„Å´ÂØæ„Åó„Å¶„ÄÅÂà§Êñ≠"$\TT{Z plus $\MV{n}$ is $\MV{n}$}$"„ÇíÂ∞é„ÅÑ„Å¶ËâØ„ÅÑ„ÄÇ
  -/
  | P_Zero (n : PNat)
    : Derivation (.Plus .Z n n)
  /--
  ‰ªªÊÑè„ÅÆ„Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞$\MV{n_1},\MV{n_2},\MV{n_3}$„Å´ÂØæ„Åó„Å¶„ÄÅÂà§Êñ≠"$\TT{$\MV{n_1}$ plus $\MV{n_2}$ is $\MV{n_3}$}$"„Åã„Çâ"$\TT{S$\MV{n_1}$ plus $\MV{n_2}$ is S$\MV{n_3}$}$"„ÇíÂ∞é„ÅÑ„Å¶ËâØ„ÅÑ„ÄÇ
  -/
  | P_Succ {n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat}
    : Derivation (.Plus n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (.Plus n‚ÇÅ.S n‚ÇÇ n‚ÇÉ.S)
  /--
  "$\TT{Z times $\MV{n}$ is Z}$"
  -/
  | T_Zero (n : PNat)
    : Derivation (.Times .Z n .Z)
  /--
  "$\TT{$\MV{n_1}$ times $\MV{n_2}$ is $\MV{n_3}$}$"„Åã„Å§"$\TT{$\MV{n_2}$ plus $\MV{n_3}$ is $\MV{n_4}$}$"„Å™„Çâ„Å∞„ÄÅ"$\TT{S$\MV{n_1}$ times $\MV{n_2}$ is $\MV{n_4}$}$"
  -/
  | T_Succ {n‚ÇÅ n‚ÇÇ n‚ÇÉ n‚ÇÑ : PNat}
    : Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (.Plus n‚ÇÇ n‚ÇÉ n‚ÇÑ) ‚Üí Derivation (.Times n‚ÇÅ.S n‚ÇÇ n‚ÇÑ)

abbrev Derivable := @HelloTypeSystem.Derivable Derivation

def Derivation.induction_plus
  {motive : PNat ‚Üí PNat ‚Üí PNat ‚Üí Sort _} -- P(n‚ÇÅ,n‚ÇÇ,n‚ÇÉ)
  {n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat}
  (hP_Zero : ‚àÄ n : PNat, motive .Z n n)
  (hP_Succ : ‚àÄ {n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat}, Derivation (.Plus n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí motive n‚ÇÅ n‚ÇÇ n‚ÇÉ ‚Üí motive n‚ÇÅ.S n‚ÇÇ n‚ÇÉ.S)
: Derivation (.Plus n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí motive n‚ÇÅ n‚ÇÇ n‚ÇÉ
  | .P_Zero n => hP_Zero n
  | .P_Succ d => hP_Succ d (Derivation.induction_plus hP_Zero hP_Succ d)

def Derivation.induction_times
  {motive : PNat ‚Üí PNat ‚Üí PNat ‚Üí Sort _} -- P(n‚ÇÅ,n‚ÇÇ,n‚ÇÉ)
  {n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat}
  (hT_Zero : ‚àÄ n : PNat, motive .Z n .Z)
  (hT_Succ : ‚àÄ {n‚ÇÅ n‚ÇÇ n‚ÇÉ n‚ÇÑ: PNat}, Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (.Plus n‚ÇÇ n‚ÇÉ n‚ÇÑ) ‚Üí motive n‚ÇÅ n‚ÇÇ n‚ÇÉ ‚Üí motive n‚ÇÅ.S n‚ÇÇ n‚ÇÑ)
: Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí motive n‚ÇÅ n‚ÇÇ n‚ÇÉ
  | .T_Zero n     => hT_Zero n
  | .T_Succ dt dp => hT_Succ dt dp (dt.induction_times hT_Zero hT_Succ)

end Nat

/-!
### „Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞„ÅÆÊØîËºÉÔºöCompareNat1--3
-/
namespace CompareNat1
/--
Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†CompareNat1„ÅÆÊé®Ë´ñË¶èÂâá
-/
inductive Derivation : Judgement ‚Üí Type where
  | LT_Succ (n : PNat)
    : Derivation (.LT n n.S)
  | LT_Trans {n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat}
    : Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí Derivation (.LT n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (.LT n‚ÇÅ n‚ÇÉ)

abbrev Derivable := @HelloTypeSystem.Derivable Derivation

/--
CompareNat1„Å´„Åä„Åë„Çã$\TT{$\MV{n_1}$ is less than $\MV{n_2}$}$„ÅÆÂ∞éÂá∫„Å´Èñ¢„Åô„ÇãÂ∏∞Á¥çÊ≥ï„Åß„ÄÅ
„Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞„Å´Èñ¢„Åô„Çã2È†ÖËø∞Ë™û$P$„Å´„Å§„ÅÑ„Å¶$\forall\MV{n_1},\MV{n_2}. \bigl[\TT{$\MV{n_1}$ is less than $\MV{n_2}$} \implies P(\MV{n_1},\MV{n_2})\bigr]$„ÇíÁ§∫„Åô„ÄÇ

Ëá™Âãï„ÅßÁîüÊàê„Åï„Çå„Çã`casesOn`„ÇÑ`rec`„Å™„Å©„ÅØ`motive`„ÅÆÂûã„Åå`(a : Judgement) ‚Üí Derivation a ‚Üí Sort u`„Å®„Å™„Å£„Å¶„ÅÑ„Å¶„ÄÅ
„Éö„Ç¢„ÉéËá™ÁÑ∂Êï∞„Å´Èñ¢„Åô„ÇãËø∞Ë™û$P(\MV{n_1},\MV{n_2})$„ÇíÊâ±„ÅÜ„Å´„ÅØ`PNat ‚Üí PNat ‚Üí Sort u`„Å™Èñ¢Êï∞„Çí‰Ωú„ÇãÂøÖË¶Å„Åå„ÅÇ„Å£„Åü„ÄÇ
-/
def Derivation.induction
  {motive : PNat ‚Üí PNat ‚Üí Sort _} -- P(n‚ÇÅ,n‚ÇÇ)
  {n‚ÇÅ n‚ÇÇ : PNat}
  (H0 : ‚àÄ n : PNat, motive n n.S)
  (H1 : ‚àÄ {n‚ÇÅ n‚ÇÇ n‚ÇÉ : PNat}, Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí Derivation (.LT n‚ÇÇ n‚ÇÉ) ‚Üí motive n‚ÇÅ n‚ÇÇ ‚Üí motive n‚ÇÇ n‚ÇÉ ‚Üí motive n‚ÇÅ n‚ÇÉ)
: Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí motive n‚ÇÅ n‚ÇÇ
  | .LT_Succ k      => H0 k
  | .LT_Trans ùíü‚ÇÅ ùíü‚ÇÇ => H1 ùíü‚ÇÅ ùíü‚ÇÇ (induction H0 H1 ùíü‚ÇÅ) (induction H0 H1 ùíü‚ÇÇ)

end CompareNat1

namespace CompareNat2
/--
Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†CompareNat2„ÅÆÊé®Ë´ñË¶èÂâá
-/
inductive Derivation : Judgement ‚Üí Type where
  | LT_Zero (n : PNat)
    : Derivation (.LT .Z n.S)
  | LT_SuccSucc {n‚ÇÅ n‚ÇÇ : PNat}
    : Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí Derivation (.LT n‚ÇÅ.S n‚ÇÇ.S)

abbrev Derivable := @HelloTypeSystem.Derivable Derivation

/--
CompareNat2„Å´„Åä„Åë„Çã$\TT{$\MV{n_1}$ is less than $\MV{n_2}$}$„ÅÆÂ∞éÂá∫„Å´Èñ¢„Åô„ÇãÂ∏∞Á¥çÊ≥ï
-/
def Derivation.induction
  {motive : PNat ‚Üí PNat ‚Üí Sort _} -- P(n‚ÇÅ,n‚ÇÇ)
  {n‚ÇÅ n‚ÇÇ : PNat}
  (H0 : ‚àÄ n : PNat, motive .Z n.S)
  (H1 : ‚àÄ {n‚ÇÅ n‚ÇÇ : PNat}, Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí motive n‚ÇÅ n‚ÇÇ ‚Üí motive n‚ÇÅ.S n‚ÇÇ.S)
: Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí motive n‚ÇÅ n‚ÇÇ
  | .LT_Zero n     => H0 n
  | .LT_SuccSucc ùíü => H1 ùíü (induction H0 H1 ùíü)

end CompareNat2

namespace CompareNat3
/--
Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†CompareNat3„ÅÆÊé®Ë´ñË¶èÂâá
-/
inductive Derivation : Judgement ‚Üí Type where
  | LT_Succ (n : PNat)
    : Derivation (.LT n n.S)
  | LT_SuccR {n‚ÇÅ n‚ÇÇ : PNat}
    : Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí Derivation (.LT n‚ÇÅ n‚ÇÇ.S)

abbrev Derivable := @HelloTypeSystem.Derivable Derivation

/--
CompareNat3„Å´„Åä„Åë„Çã$\TT{$\MV{n_1}$ is less than $\MV{n_2}$}$„ÅÆÂ∞éÂá∫„Å´Èñ¢„Åô„ÇãÂ∏∞Á¥çÊ≥ï
-/
def Derivation.induction
  {motive : PNat ‚Üí PNat ‚Üí Sort _} -- P(n‚ÇÅ,n‚ÇÇ)
  {n‚ÇÅ n‚ÇÇ : PNat}
  (H0 : ‚àÄ n : PNat, motive n n.S)
  (H1 : ‚àÄ {n‚ÇÅ n‚ÇÇ : PNat}, Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí motive n‚ÇÅ n‚ÇÇ ‚Üí motive n‚ÇÅ n‚ÇÇ.S)
: Derivation (.LT n‚ÇÅ n‚ÇÇ) ‚Üí motive n‚ÇÅ n‚ÇÇ
  | .LT_Succ n  => H0 n
  | .LT_SuccR ùíü => H1 ùíü (induction H0 H1 ùíü)

end CompareNat3

/-!
### ÁÆóË°ìÂºè„ÅÆË©ï‰æ°
-/

namespace EvalNatExpr
/--
Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†EvalNatExpr„ÅÆÊé®Ë´ñË¶èÂâá
-/
inductive Derivation : Judgement ‚Üí Type where
  | P_Zero (n : PNat)
    : Derivation (.Plus .Z n n)
  | P_Succ {n‚ÇÅ n‚ÇÇ n}
    : Derivation (.Plus n‚ÇÅ n‚ÇÇ n) ‚Üí Derivation (.Plus n‚ÇÅ.S n‚ÇÇ n.S)
  | T_Zero (n : PNat)
    : Derivation (.Times .Z n .Z)
  | T_Succ {n‚ÇÅ n‚ÇÇ n‚ÇÉ n‚ÇÑ : PNat}
    : Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (.Plus n‚ÇÇ n‚ÇÉ n‚ÇÑ) ‚Üí Derivation (.Times n‚ÇÅ.S n‚ÇÇ n‚ÇÑ)
  | E_Const (n : PNat)
    : Derivation (n ‚áì n)
  | E_Add
    : Derivation (e‚ÇÅ ‚áì n‚ÇÅ) ‚Üí Derivation (e‚ÇÇ ‚áì n‚ÇÇ) ‚Üí Derivation (.Plus n‚ÇÅ n‚ÇÇ n) ‚Üí Derivation (e‚ÇÅ + e‚ÇÇ ‚áì n)
  | E_Mul
    : Derivation (e‚ÇÅ ‚áì n‚ÇÅ) ‚Üí Derivation (e‚ÇÇ ‚áì n‚ÇÇ) ‚Üí Derivation (.Times n‚ÇÅ n‚ÇÇ n) ‚Üí Derivation (e‚ÇÅ * e‚ÇÇ ‚áì n)

abbrev Derivable := @HelloTypeSystem.Derivable Derivation

end EvalNatExpr

namespace ReduceNatExpr
/--
Â∞éÂá∫„Ç∑„Çπ„ÉÜ„É†ReduceNatExpr„ÅÆÊé®Ë´ñË¶èÂâá
-/
inductive Derivation : Judgement ‚Üí Type where
  | P_Zero (n : PNat)
    : Derivation (.Plus 0 n n)
  | P_Succ {n‚ÇÅ n‚ÇÇ n}
    : Derivation (.Plus n‚ÇÅ n‚ÇÇ n) ‚Üí Derivation (.Plus n‚ÇÅ.S n‚ÇÇ n.S)
  | T_Zero (n : PNat)
    : Derivation (.Times 0 n 0)
  | T_Succ {n‚ÇÅ n‚ÇÇ n‚ÇÉ n‚ÇÑ : PNat}
    : Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (.Plus n‚ÇÇ n‚ÇÉ n‚ÇÑ) ‚Üí Derivation (.Times n‚ÇÅ.S n‚ÇÇ n‚ÇÑ)
  | R_Plus
    : Derivation (.Plus n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (n‚ÇÅ + n‚ÇÇ ‚ü∂ n‚ÇÉ)
  | R_Times
    : Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (n‚ÇÅ * n‚ÇÇ ‚ü∂ n‚ÇÉ)
  | R_PlusL
    : Derivation (e‚ÇÅ ‚ü∂ e‚ÇÅ') ‚Üí Derivation (e‚ÇÅ + e‚ÇÇ ‚ü∂ e‚ÇÅ' + e‚ÇÇ)
  | R_PlusR
    : Derivation (e‚ÇÇ ‚ü∂ e‚ÇÇ') ‚Üí Derivation (e‚ÇÅ + e‚ÇÇ ‚ü∂ e‚ÇÅ + e‚ÇÇ')
  | R_TimesL
    : Derivation (e‚ÇÅ ‚ü∂ e‚ÇÅ') ‚Üí Derivation (e‚ÇÅ * e‚ÇÇ ‚ü∂ e‚ÇÅ' * e‚ÇÇ)
  | R_TimesR
    : Derivation (e‚ÇÇ ‚ü∂ e‚ÇÇ') ‚Üí Derivation (e‚ÇÅ * e‚ÇÇ ‚ü∂ e‚ÇÅ * e‚ÇÇ')
  | MR_Zero
    : Derivation (e ‚ü∂* e)
  | MR_Once
    : Derivation (e ‚ü∂ e') ‚Üí Derivation (e ‚ü∂* e')
  | MR_Multi
    : Derivation (e ‚ü∂* e') ‚Üí Derivation (e' ‚ü∂* e'') ‚Üí Derivation (e ‚ü∂* e'')
  | DR_Plus
    : Derivation (.Plus n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (n‚ÇÅ + n‚ÇÇ ‚ü∂' n‚ÇÉ)
  | DR_Times
    : Derivation (.Times n‚ÇÅ n‚ÇÇ n‚ÇÉ) ‚Üí Derivation (n‚ÇÅ * n‚ÇÇ ‚ü∂' n‚ÇÉ)
  | DR_PlusL
    : Derivation (e‚ÇÅ ‚ü∂' e‚ÇÅ') ‚Üí Derivation (e‚ÇÅ + e‚ÇÇ ‚ü∂' e‚ÇÅ' + e‚ÇÇ)
  | DR_PlusR {n‚ÇÅ : PNat}
    : Derivation (e‚ÇÇ ‚ü∂' e‚ÇÇ') ‚Üí Derivation (n‚ÇÅ + e‚ÇÇ ‚ü∂' n‚ÇÅ + e‚ÇÇ')
  | DR_TimesL
    : Derivation (e‚ÇÅ ‚ü∂' e‚ÇÅ') ‚Üí Derivation (e‚ÇÅ * e‚ÇÇ ‚ü∂' e‚ÇÅ' * e‚ÇÇ)
  | DR_TimesR {n‚ÇÅ : PNat}
    : Derivation (e‚ÇÇ ‚ü∂' e‚ÇÇ') ‚Üí Derivation (n‚ÇÅ * e‚ÇÇ ‚ü∂' n‚ÇÅ * e‚ÇÇ')

end ReduceNatExpr
